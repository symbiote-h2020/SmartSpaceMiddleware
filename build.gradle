// initial extensions to gradle
if (!hasProperty('mainClass')) {
    ext.mainClass = 'eu.h2020.symbiote.ssp.SspApplication'
}

buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.2.1'
        classpath 'org.owasp:dependency-check-gradle:1.4.5.1'
    }
}

// code
apply plugin: "io.spring.dependency-management"
apply plugin: 'java'
apply plugin: 'org.springframework.boot'

// code quality
apply plugin: 'jacoco'
apply plugin: 'org.owasp.dependencycheck'

// IDEs
apply plugin: 'eclipse'
apply plugin: 'idea'

// publishing
apply plugin: 'com.cinnober.gradle.semver-git'

// XXX please change with your commits according to http://semver.org/
project.version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// dependencies section
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

// Spring related configs
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.RELEASE"
    }
}

/*
Forces dependencies' cache invalidation for each build for dependencies marked with
    { changing = true }
    e.g.
    compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT'){ changing = true }
 */
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    /* SymbIoTeLibraries manual:
        One can use the library from jitpack by default with the notation:
            compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT') {changing = true }
        or having cloned locally the SymbIoTeLibraries repo use it directly with project dependency by:
            compile project(':SymbIoTeLibraries')
        Important --- In order to use the latter you need to:
            1) switch comments on the artifact and project dependencies
            2) always have only one uncommented
            3) project dependency requires changes in settings.gradle file in this project
            4) never commit build.gradle which has project dependencies active as it will break CI builds
    */
    //compile project(':SymbIoTeLibraries')
    compile('com.github.symbiote-h2020:SymbIoTeLibraries:WP4_demo-SNAPSHOT') { changing = true }
    compile('com.github.symbiote-h2020:SymbIoTeSecurity:23.+'){ changing = true }
    // allows to select the downloaded matching sources in IntelliJ Idea as it fails to do so automatically from jitpack repo
    compile('com.github.symbiote-h2020:SymbIoTeSecurity:23.+:sources'){ changing = true }
    //compile project(':SymbIoTeSecurity')    
    // compile('com.github.symbiote-h2020:SymbIoTeLibraries:0.2.1')

    // Spring
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework:spring-web')

    // OData
    compile("org.apache.olingo:odata-commons-api:${'4.3.0'}")
    compile("org.apache.olingo:odata-commons-core:${'4.3.0'}")

    compile("org.apache.olingo:odata-server-api:${'4.3.0'}")
    compile("org.apache.olingo:odata-server-core:${'4.3.0'}")

    //Owlapi
    compile('net.sourceforge.owlapi:owlapi-distribution:5.1.0')
    compile('net.sourceforge.owlapi:owlapi-contract:3.4')

    // Other
    compile('org.apache.commons:commons-lang3:3.0')

    // tests only
    testCompile('junit:junit:4.+')
    testCompile('org.springframework.boot:spring-boot-starter-test')

}

// code quality below
task generateJavaDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = reporting.file("javadocs")
}

// jacoco configuration section
jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// owasp config
dependencyCheck {
    outputDirectory = "build/reports/security"
}

// including code quality extensions into the build
check.dependsOn(jacocoTestReport, 'dependencyCheck')
